plugins {
	id "com.github.node-gradle.node" version "2.2.0"
	id 'signing'
	id 'maven'
}

//apply plugin: 'base'

apply from: "${rootProject.projectDir}/gradle/sonatype-resource.gradle"

node {
    version = '12.18.3'
    download = true
}

yarn_build {
    inputs.files fileTree("src")
    inputs.file 'package.json'
    inputs.file 'yarn.lock'
    inputs.file 'tsconfig.json'
    outputs.dir "${projectDir}/build"
}

task copyDist(type: Copy) {
	dependsOn yarn_build
	from 'build'
    into "${projectDir}/dist/classes/META-INF/resources/admin"
}
task copyIndex(type: Copy) {
	dependsOn copyDist
	from 'build/index.html'
	into "${projectDir}/dist/classes/META-INF/templates/admin"
}
task packageNpmApp(type: Zip) {
    dependsOn copyIndex
    baseName 'sprout-admin'
    extension 'jar'
    destinationDir file("${projectDir}/target/")
    from("${projectDir}/dist/classes/") {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        // into 'static' 
    }
}

configurations {
    npmResources
}
configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type "jar"
    }
}

assemble.dependsOn packageNpmApp
uploadArchives.dependsOn build

clean {
    delete packageNpmApp.archivePath
    delete 'dist'
    delete 'build'
    delete 'target'
}