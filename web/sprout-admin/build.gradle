buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}
apply plugin: 'base'
apply plugin: 'com.moowork.node' // gradle-node-plugin


node {
    /* gradle-node-plugin configuration
       https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
       Task name pattern:
       ./gradlew npm_<command> Executes an NPM command.
    */
    // Version of node to use.
    version = '10.14.1'
    // Version of npm to use.
    npmVersion = '6.4.1'
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
}

npm_run_build {
    inputs.files fileTree("src")
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir 'target'
}
task copyDist(type: Copy) {
	dependsOn npm_run_build
	from 'dist'
    into "$buildDir/classes/META-INF/resources/admin"
}
task copyIndex(type: Copy) {
	dependsOn copyDist
	from 'dist/index.html'
	into "$buildDir/classes/META-INF/templates/admin"
}
task packageNpmApp(type: Zip) {
    dependsOn copyDist
    baseName 'sprout-admin'
    extension 'jar'
    destinationDir file("$buildDir/")
    from("$buildDir/classes/") {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        // into 'static' 
    }
}

configurations {
    npmResources
}
configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type "jar"
    }
}

assemble.dependsOn packageNpmApp

clean {
    delete packageNpmApp.archivePath
}